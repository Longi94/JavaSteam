//file:noinspection GroovyAssignabilityCheck

plugins {
    alias(libs.plugins.protobuf.gradle)
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())

repositories {
    mavenCentral()
}

protobuf.protoc {
    artifact = libs.protobuf.protoc.get()
}

javadoc {
    exclude "**/in/dragonbra/javasteam/protobufs/**"
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    implementation libs.protobuf.java
}

/* Artifact publishing */

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'JavaSteam-tf'
                packaging = 'jar'
                description = 'TF classes for JavaSteam.'
                url = 'https://github.com/Longi94/JavaSteam'
                inceptionYear = '2018'

                scm {
                    connection = 'scm:git:git://github.com/Longi94/JavaSteam.git'
                    developerConnection = 'scm:git:ssh://github.com:Longi94/JavaSteam.git'
                    url = 'http://github.com/Longi94/JavaSteam/tree/master'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'Longi'
                        name = 'Long Tran'
                        email = 'lngtrn94@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def stagingRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotRepoUrl : stagingRepoUrl
            credentials {
                username = ossrhUsername ?: ''
                password = ossrhPassword ?: ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
